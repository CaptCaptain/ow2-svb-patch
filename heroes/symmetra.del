playervar Player SymmetraBotPlayer;

void CleanupSymmetra() playervar "[symmetra.del] Subroutine: Cleanup" {
    StoppedBeingHero();

    DestroyDummyBot(TeamOf(SymmetraBotPlayer), SlotOf(SymmetraBotPlayer));

    CleanupEffects();
    CleanupTexts();

    AllowButton(Button: Button.Ultimate);
    SetUltimateAbilityEnabled(Enabled: true);

    Wait();

    HeroActive = null;
}

void CreateSymmetraBotEffects() playervar "[symmetra.del] Subroutine: Create Bot Effects" {
    Wait(0.1);
    
    CreateEffect(AllPlayers(), Effect.EchoCloningEffect, <Color>TeamOf(), SymmetraBotPlayer, 1, EffectRev.VisibleTo);
    ModifyVariable(HeroEffects, Operation.AppendToArray, LastCreatedEntity());

    Wait(0.1);

    CreateEffect(AllPlayers(), Effect.WinstonTeslaCannonTargetSound, <Color>TeamOf(), SymmetraBotPlayer, 700, EffectRev.VisibleTo);
    ModifyVariable(HeroEffects, Operation.AppendToArray, LastCreatedEntity());

    Wait(0.1);
    
    for (define repeat = 0; 3; 1)
    {
        CreateEffect(AllPlayers(), Effect.BadAura, <Color>TeamOf(), SymmetraBotPlayer, 0, EffectRev.VisibleTo);
        ModifyVariable(HeroEffects, Operation.AppendToArray, LastCreatedEntity());

        Wait(0.0833333333);
    }
}

void SymmetraButtonMacro(Button button) {
    WaitUntil(!EntityExists(SymmetraBotPlayer) || EventPlayer().IsButtonHeld(button), 9999);
   
    AbortIf(!EntityExists(SymmetraBotPlayer));
    StartHoldingButton(SymmetraBotPlayer, button);
    
    WaitUntil(!EntityExists(SymmetraBotPlayer) || !EventPlayer().IsButtonHeld(button), 9999);
    StopHoldingButton(SymmetraBotPlayer, button);
    
    Wait();
    LoopIf(EntityExists(SymmetraBotPlayer));
}

void SymmetraBotPrimaryFire() playervar "[symmetra.del] Subroutine: Bot Copy Primary Fire" { SymmetraButtonMacro(Button.PrimaryFire); }
void SymmetraBotSecondaryFire() playervar "[symmetra.del] Subroutine: Bot Copy Secondary Fire" { SymmetraButtonMacro(Button.SecondaryFire); }
void SymmetraBotCrouch() playervar "[symmetra.del] Subroutine: Bot Copy Crouch" { SymmetraButtonMacro(Button.Crouch); }
void SymmetraBotJump() playervar "[symmetra.del] Subroutine: Bot Copy Jump" { SymmetraButtonMacro(Button.Jump); }

void InitialSymmetra() playervar "[symmetra.del] Subroutine: Initial" {
    InitialHeroSetup(Hero.Symmetra);

    DisallowButton(Button: Button.Ultimate);

    # Create Hud
    CreateHudText(EventPlayer(), null, null, <"<0>(<1>)", <" \r\n \r\n \r\n \r\n \r\n \r\n \r\n \r\n \r\n \r\n \r\n \r\n \r\n \r\n \r\n \r\n \r\n \r\n \r\n \r\n \r\n \r\n \r\n \r\n \r\n \r\n \r\n \r\n \r\n \r\n \r\n \r\n <0>", <"                                                                                                                            <0>", <"                                                                   <0>", " ">>>, "Light Clone">, Location.Left, 1, Color.White, Color.White, Color.White, HudTextRev.VisibleTo, Spectators.DefaultVisibility);
    ModifyVariable(HeroTexts, Operation.AppendToArray, LastTextID());

    async! CleanupSymmetra();
}

void SymmetraLightCloneUlt() playervar "[symmetra.del] Subroutine: Ult" {
    SetUltimateCharge(ChargePercent: 0);
    SetUltimateAbilityEnabled(Enabled: false);

    # Create bot
    CreateDummyBot(Hero.Symmetra, TeamOf(), SlotOf() + 5, PositionOf(), FacingDirectionOf());
    SymmetraBotPlayer = LastCreatedEntity();

    SetInvisible(SymmetraBotPlayer, InvisibleTo.All);

    # Wait for bot to spawn
    WaitUntil(IsAlive(SymmetraBotPlayer), 1);
    WaitUntil(HasSpawned(SymmetraBotPlayer), 1);

    # If the bot doesn't spawn, return ult back to Symmetra and abort
    SkipIf(EntityExists(SymmetraBotPlayer), 3);
    SetUltimateCharge(ChargePercent: 100);
    SetUltimateAbilityEnabled(Enabled: true);
    Abort();

    Wait();

    StartForcingDummyBotName(SymmetraBotPlayer, <"Light Clone">);
    SetAmmo(SymmetraBotPlayer, 0, Ammo(Clip: 0));
    SetDamageDealt(SymmetraBotPlayer, 50);
    SetUltimateAbilityEnabled(SymmetraBotPlayer, false);
    Teleport(SymmetraBotPlayer, PositionOf());

    Wait(0.08);

    # Create effects
    async! CreateSymmetraBotEffects();

    # Casting
    PlayEffect(AllPlayers(), PlayEffect.RingExplosion, <Color>TeamOf(), PositionOf(SymmetraBotPlayer), 10);
    PlayEffect(AllPlayers(), PlayEffect.SombraTranslocatorDisappearSound, <Color>TeamOf(), EyePosition(SymmetraBotPlayer), 500);
    PlayEffect(AllPlayers(), PlayEffect.DoomfistRisingUppercutImpactSound, <Color>TeamOf(), EyePosition(SymmetraBotPlayer), 500);

    SetInvisible(SymmetraBotPlayer, InvisibleTo.None);

    Communicate(EventPlayer(), Communication.Attacking);

    # Wait until the bot is dead or after 12 seconds have elapsed
    WaitUntil(IsDead(SymmetraBotPlayer) || IsDead(), 12);
    Wait();

    # Cleanup
    PlayEffect(AllPlayers(), PlayEffect.BadExplosion, <Color>TeamOf(), PositionOf(SymmetraBotPlayer), 10);
    PlayEffect(AllPlayers(), PlayEffect.SombraTranslocatorDisappearSound, <Color>TeamOf(), EyePosition(SymmetraBotPlayer), 500);
    PlayEffect(AllPlayers(), PlayEffect.DoomfistRisingUppercutImpactSound, <Color>TeamOf(), EyePosition(SymmetraBotPlayer), 500);
    DestroyDummyBot(TeamOf(SymmetraBotPlayer), SlotOf(SymmetraBotPlayer));
    
    SetUltimateAbilityEnabled(Enabled: true);
    SymmetraBotPlayer = [];

    CleanupEffects();
}

void UpdateSymmetraAI() playervar "[symmetra.del] Subroutine: Update Symmetra AI" {
    # Mirror the player's movement
    StartThrottleInDirection(SymmetraBotPlayer, DirectionTowards(WorldVectorOf(Vector(), EventPlayer(), LocalVector.RotationAndTranslation), WorldVectorOf(Vector(-1 * ThrottleOf().X, 0, ThrottleOf().Z), EventPlayer(), LocalVector.RotationAndTranslation)), HorizontalSpeedOf(), Relative.ToWorld, ThrottleBehavior.ReplaceExistingThrottle);

    StartFacing(SymmetraBotPlayer, UpdateEveryFrame(DirectionTowards(EyePosition(SymmetraBotPlayer), RayCastHitPosition(EyePosition(), EyePosition() + (FacingDirectionOf() * 500), AllPlayers(OppositeTeamOf(TeamOf())), [SymmetraBotPlayer, EventPlayer()], false))), 1000, Relative.ToWorld, FacingRev.DirectionAndTurnRate);

    DisableMovementCollisionWithPlayers(SymmetraBotPlayer);
    Teleport(SymmetraBotPlayer, PositionOf());
    StartModifyingHeroVoiceLines(SymmetraBotPlayer, RandomReal(0, 2), false);

    async SymmetraBotPrimaryFire();
    async SymmetraBotSecondaryFire();
    async SymmetraBotJump();
    async SymmetraBotCrouch();
}

/* ================================================== */

rule: '[symmetra.del] Initial'
Event.OngoingPlayer
Player.Symmetra
{
    InitialSymmetra();
}

rule: '[symmetra.del] Light Clone Ult'
Event.OngoingPlayer
Player.Symmetra
if (IsButtonHeld(Button: Button.Ultimate))
if (IsAlive())
if (UltimateChargePercent() == 100)
{
    SymmetraLightCloneUlt();
}

rule: '[symmetra.del] Light Clone AI'
Event.OngoingPlayer
Player.Symmetra
if (EntityExists(SymmetraBotPlayer))
{
    UpdateSymmetraAI();
}

/* ================================================== */

rule: '[symmetra.del] Initial (Duplication)'
Event.OngoingPlayer
Player.Echo
if (HeroBeingDuplicated() == Hero.Symmetra)
{
    InitialSymmetra();
}

rule: '[symmetra.del] Light Clone Ult (Duplication)'
Event.OngoingPlayer
Player.Echo
if (HeroBeingDuplicated() == Hero.Symmetra)
if (IsButtonHeld(Button: Button.Ultimate))
if (UltimateChargePercent() == 100)
{
    SymmetraLightCloneUlt();
}

rule: '[symmetra.del] Light Clone AI (Duplication)'
Event.OngoingPlayer
Player.Echo
if (HeroBeingDuplicated() == Hero.Symmetra)
if (EntityExists(SymmetraBotPlayer))
{
    UpdateSymmetraAI();
}