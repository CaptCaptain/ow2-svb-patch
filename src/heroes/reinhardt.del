playervar define ReinhardtFirestrikeCharges = 2;
playervar define ReinhardtFirestrikeCooldown = 6;
playervar define facingAngle;

/* ======================================================== */

void CleanupReinhardt() "[reinhardt.del] Subroutine: Cleanup" {
    WaitUntil(HeroOf() != EvaluateOnce(HeroOf()), 9999);

    CleanupHealthPools();
    CleanupTexts();
    SetMaxHealth(HealthPercent: 100);
    SetAbility2Enabled(Enabled: true);
    StopChasingVariable(ReinhardtFirestrikeCooldown);
    StopAccelerating();

    Wait();

    ModifyVariable(ActiveHero, Operation.RemoveFromArrayByValue, Hero.Reinhardt);
}

void InitialReinhardt() '[reinhardt.del] Subroutine: Initial' {
    InitialHeroSetup(Hero.Reinhardt);

    Wait();
    
    # Variable setup
    ReinhardtFirestrikeCharges = 2;
    ReinhardtFirestrikeCooldown = 6;
    ChaseVariableAtRate(ReinhardtFirestrikeCooldown, 0, 1, RateChaseReevaluation.DestinationAndRate);
    
    # Character setup
    SetMaxHealth(EventPlayer(), 50);
    AddHealthPoolToPlayer(EventPlayer(), HealthType.Health, 150, true, true);
    ModifyVariable(HeroHealthPools, Operation.AppendToArray, LastCreatedHealthPool());
    AddHealthPoolToPlayer(EventPlayer(), HealthType.Armor, 125, true, true);
    ModifyVariable(HeroHealthPools, Operation.AppendToArray, LastCreatedHealthPool());

    // Create hud text
    CreateHudText(EventPlayer(), null, null, <"<0>(<1>) [<2>]", <" \r\n \r\n \r\n \r\n \r\n \r\n \r\n \r\n \r\n \r\n \r\n \r\n \r\n \r\n \r\n \r\n \r\n \r\n \r\n \r\n \r\n \r\n \r\n \r\n \r\n \r\n \r\n \r\n \r\n \r\n \r\n \r\n \r\n \r\n \r\n <0>", <"                                                                                                                            <0>", <"                                                                                                                            <0>", "                                                                         ">>>, RoundToInteger(ReinhardtFirestrikeCharges, Rounding.Down) > 0 ? RoundToInteger(ReinhardtFirestrikeCharges, Rounding.Down) : IconString(Icon.X), RoundToInteger(ReinhardtFirestrikeCharges, Rounding.Down) == 2 ? IconString(Icon.Checkmark) : RoundToInteger(ReinhardtFirestrikeCooldown, Rounding.Up)>, Location.Left, 1, Color.White, Color.White, Color.White, HudTextRev.VisibleToAndString, Spectators.DefaultVisibility);
    ModifyVariable(HeroTexts, Operation.AppendToArray, LastTextID());

    # Start Rule
    async! CleanupReinhardt();
}

void ReinhardtChargeCancelling() '[reinhardt.del] Subroutine: Charge Cancelling' {
    Wait(1);
    WaitUntil(!IsButtonHeld(EventPlayer(), Button.Ability1) || !IsUsingAbility1(EventPlayer()), 99999);
    WaitUntil(!IsUsingAbility1(EventPlayer()) || IsButtonHeld(EventPlayer(), Button.Ability1), 99999);
    CancelPrimaryAction(EventPlayer());
}

void ReinhardtLowerChargeCooldown() '[reinhardt.del] Subroutine: Lower Charge Cooldown' {
    WaitUntil(IsUsingAbility1(EventPlayer()) == false, 9999);
    SetAbilityCooldown(EventPlayer(), Button.Ability1, 8);
}

void ReinhardtRefreshFirestrike() '[reinhardt.del] Subroutine: Refresh Firestrike' {
    WaitUntil(IsUsingAbility2(EventPlayer()) == false, 9999);
    SetAbilityCooldown(EventPlayer(), Button.Ability2, 0);
}

void ReinhardtFirestrikeRecharge() '[reinhardt.del] Subroutine: Firestrike Recharge' {
    ReinhardtFirestrikeCooldown = 6;
    WaitUntil(ReinhardtFirestrikeCooldown == 0, 9999);
    ReinhardtFirestrikeCharges = Min(ReinhardtFirestrikeCharges + 1, 2);
    LoopIfConditionIsTrue();
}

void ReinhardtDetectFacingAngle() '[reinhardt.del] Subroutine: Detect Facing Angle' {
    facingAngle = HorizontalFacingAngleOf(EventPlayer());
    MinWait();
    # steering right
    if (AngleDifference(HorizontalFacingAngleOf(EventPlayer()), facingAngle) > 0)
    {
        if (IsInAir(EventPlayer()))
        {
            StartAccelerating(EventPlayer(), Right(), 100, 4, Relative.ToPlayer, AccelerateRev.DirectionRateAndMaxSpeed);
        }
        else
        {
            StartAccelerating(EventPlayer(), Right(), 500, 11.5, Relative.ToPlayer, AccelerateRev.DirectionRateAndMaxSpeed);
        }
    }
    else if (AngleDifference(HorizontalFacingAngleOf(EventPlayer()), facingAngle) < 0)
    {
        if (IsInAir(EventPlayer()))
        {
            StartAccelerating(EventPlayer(), Left(), 100, 4, Relative.ToPlayer, AccelerateRev.DirectionRateAndMaxSpeed);
        }
        else
        {
            StartAccelerating(EventPlayer(), Left(), 500, 11.5, Relative.ToPlayer, AccelerateRev.DirectionRateAndMaxSpeed);
        }
    }
    else
    {
        StopAccelerating(EventPlayer());
    }
    LoopIfConditionIsTrue();
    StopAccelerating();
}

/* ======================================================== */

rule: "[reinhardt.del] Initial"
Event.OngoingPlayer
Player.Reinhardt
{
    InitialReinhardt();
}

rule: "[reinhardt.del] Charge Cancelling"
Event.OngoingPlayer
Player.Reinhardt
if (IsUsingAbility1(EventPlayer()) == true)
{
    ReinhardtChargeCancelling();
}

rule: "[reinhardt.del] Lower Charge Cooldown"
Event.OngoingPlayer
Player.Reinhardt
if (IsUsingAbility1(EventPlayer()) == true)
{
    ReinhardtLowerChargeCooldown();
}

rule: "[reinhardt.del] Refresh Firestrike"
Event.OngoingPlayer
Player.Reinhardt
if (IsUsingAbility2(EventPlayer()) == true)
{
    ReinhardtRefreshFirestrike();
}

rule: "[reinhardt.del] Firestrike Recharge"
Event.OngoingPlayer
Player.Reinhardt
if (RoundToInteger(ReinhardtFirestrikeCharges, Rounding.Down) < 2)
{
    ReinhardtFirestrikeRecharge();
}

rule: "[reinhardt.del] Firestrike Expends Charge"
Event.OngoingPlayer
Player.Reinhardt
if (IsUsingAbility2(EventPlayer()) == true)
{
    ReinhardtFirestrikeCharges -= 1;
}

rule: "[reinhardt.del] Enable Firestrike when charges are available"
Event.OngoingPlayer
Player.Reinhardt
if (RoundToInteger(ReinhardtFirestrikeCharges, Rounding.Down) > 0)
{
    SetAbility2Enabled(EventPlayer(), true);
}

rule: "[reinhardt.del] Disable Firestrike when charges are not available"
Event.OngoingPlayer
Player.Reinhardt
if (RoundToInteger(ReinhardtFirestrikeCharges, Rounding.Down) <= 0)
{
    SetAbility2Enabled(EventPlayer(), false);
}

rule: "[reinhardt.del] Detect Facing Angle"
Event.OngoingPlayer
Player.Reinhardt
if (IsUsingAbility1(EventPlayer()) == true)
{
    ReinhardtDetectFacingAngle();
}

/* ======================================================== */

rule: "[reinhardt.del] Initial (Duplication)"
Event.OngoingPlayer
Player.Echo
if (HeroBeingDuplicated() == Hero.Reinhardt)
{
    InitialReinhardt();
}

rule: "[reinhardt.del] Charge Cancelling (Duplication)"
Event.OngoingPlayer
Player.Echo
if (HeroBeingDuplicated() == Hero.Reinhardt)
if (IsUsingAbility1(EventPlayer()) == true)
{
    ReinhardtChargeCancelling();
}

rule: "[reinhardt.del] Lower Charge Cooldown (Duplication)"
Event.OngoingPlayer
Player.Echo
if (HeroBeingDuplicated() == Hero.Reinhardt)
if (IsUsingAbility1(EventPlayer()) == true)
{
    ReinhardtLowerChargeCooldown();
}

rule: "[reinhardt.del] Refresh Firestrike (Duplication)"
Event.OngoingPlayer
Player.Echo
if (HeroBeingDuplicated() == Hero.Reinhardt)
if (IsUsingAbility2(EventPlayer()) == true)
{
    ReinhardtRefreshFirestrike();
}

rule: "[reinhardt.del] Firestrike Recharge (Duplication)"
Event.OngoingPlayer
Player.Echo
if (HeroBeingDuplicated() == Hero.Reinhardt)
if (RoundToInteger(ReinhardtFirestrikeCharges, Rounding.Down) < 2)
{
    ReinhardtFirestrikeRecharge();
}

rule: "[reinhardt.del] Firestrike Expends Charge (Duplication)"
Event.OngoingPlayer
Player.Echo
if (HeroBeingDuplicated() == Hero.Reinhardt)
if (IsUsingAbility2(EventPlayer()) == true)
{
    ReinhardtFirestrikeCharges -= 1;
}

rule: "[reinhardt.del] Enable Firestrike when charges are available (Duplication)"
Event.OngoingPlayer
Player.Echo
if (HeroBeingDuplicated() == Hero.Reinhardt)
if (RoundToInteger(ReinhardtFirestrikeCharges, Rounding.Down) > 0)
{
    SetAbility2Enabled(EventPlayer(), true);
}

rule: "[reinhardt.del] Disable Firestrike when charges are not available (Duplication)"
Event.OngoingPlayer
Player.Echo
if (HeroBeingDuplicated() == Hero.Reinhardt)
if (RoundToInteger(ReinhardtFirestrikeCharges, Rounding.Down) <= 0)
{
    SetAbility2Enabled(EventPlayer(), false);
}

rule: "[reinhardt.del] Detect Facing Angle (Duplication)"
Event.OngoingPlayer
Player.Echo
if (HeroBeingDuplicated() == Hero.Reinhardt)
if (IsUsingAbility1(EventPlayer()) == true)
{
    ReinhardtDetectFacingAngle();
}