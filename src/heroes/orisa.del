playervar Boolean CastingUltimate;
playervar Number OrisaMoveSpeedID;
playervar Boolean Galloping;

void CreateHeroEffects() playervar "[orisa.del] Subroutine: Create Effects" {
    CreateEffect(AllPlayers(TeamOf()), Effect.GoodAura, Color.LimeGreen, UpdateEveryFrame(EyePosition()), 0.5, EffectRev.VisibleToPositionAndRadius);
    ModifyVariable(HeroEffects, Operation.AppendToArray, LastCreatedEntity());
    CreateEffect(AllPlayers(OppositeTeamOf(TeamOf())), Effect.GoodAura, Color.Red, UpdateEveryFrame(EyePosition()), 0.5, EffectRev.VisibleToPositionAndRadius);
    ModifyVariable(HeroEffects, Operation.AppendToArray, LastCreatedEntity());
    CreateEffect(AllPlayers(), Effect.WinstonPrimalRageEffect, <Color>TeamOf(), UpdateEveryFrame(EyePosition()), 1, EffectRev.VisibleToPositionAndRadius);
    ModifyVariable(HeroEffects, Operation.AppendToArray, LastCreatedEntity());
    CreateEffect(AllPlayers(), Effect.EnergySound, <Color>TeamOf(), UpdateEveryFrame(EyePosition()), 500, EffectRev.VisibleToPositionAndRadius);
    ModifyVariable(HeroEffects, Operation.AppendToArray, LastCreatedEntity());
}

void CleanupOrisa() playervar "[orisa.del] Subroutine: Cleanup" {
    StoppedBeingHero();

    CleanupTexts();
    CleanupEffects();

    AllowButton(Button: Button.Ability2);
    AllowButton(Button: Button.Ultimate);
    CleanupMoveSpeedMods();
    SetProjectileGravity(ProjectileGravityPercent: 100);
    SetDamageDealt(DamageDealtPercent: 100);
    SetProjectileSpeed(ProjectileSpeedPercent: 100);
    SetPrimaryFireEnabled(Enabled: true);
    SetSecondaryFireEnabled(Enabled: true);
    SetAbility2Enabled(Enabled: true);
    StopForcingThrottle();
    StopFacing();
    StopCamera();
    EnableMovementCollisionWithPlayers();

    Wait();

    HeroActive = null;
}

void InitialOrisa() playervar "[orisa.del] Subroutine: Initial" {
    InitialHeroSetup(Hero.Orisa);

    DisallowButton(Button: Button.Ability2);

    Wait();

    # Create Hud
    CreateHudText(EventPlayer(), null, null, <"<0>(<1>)", <" \r\n \r\n \r\n \r\n \r\n \r\n \r\n \r\n \r\n \r\n \r\n \r\n \r\n \r\n \r\n \r\n \r\n \r\n \r\n \r\n \r\n \r\n \r\n \r\n \r\n \r\n \r\n \r\n \r\n \r\n \r\n \r\n \r\n \r\n \r\n <0>", <"                                                                                                                            <0>", <"                                                                                                                            <0>", "                                                                             ">>>, "Gallop">, Location.Left, 1, Color.White, Color.White, Color.White, HudTextRev.VisibleTo, Spectators.DefaultVisibility);
    ModifyVariable(HeroTexts, Operation.AppendToArray, LastTextID());

    SetProjectileSpeed(ProjectileSpeedPercent: 125);

    CustomModifications.StartModifyingMoveSpeed(EventPlayer(), 100);
    OrisaMoveSpeedID = MoveSpeedModification.LastMoveSpeedModificationID;
    ModifyVariable(HeroMoveSpeedModifications, Operation.AppendToArray, MoveSpeedModification.LastMoveSpeedModificationID);

    async! CleanupOrisa();
}

void OrisaDecreaseMovementPenalty() playervar "[orisa.del] Subroutine: Decrease Primary Fire Movement Speed Penalty" {
    (<MoveSpeedModification>MoveSpeedModification.GetMoveSpeedModification(OrisaMoveSpeedID)).Value = 120;
    WaitUntil(!IsFiringPrimary(), 9999);
    (<MoveSpeedModification>MoveSpeedModification.GetMoveSpeedModification(OrisaMoveSpeedID)).Value = 100;
}

void OrisaGallop() playervar "[orisa.del] Subroutine: Gallop" {
    AbortIfImmobile();

    # Communication
    SetDamageDealt(DamageDealtPercent: 0);
    PressButton(Button: Button.Melee);
    CancelPrimaryAction();
    SetAbilityCooldown(Button: Button.Melee, Cooldown: 0);

    # Disable
    SetPrimaryFireEnabled(Enabled: false);
    SetSecondaryFireEnabled(Enabled: false);
    SetAbility2Enabled(Enabled: false);
    DisallowButton(Button: Button.Ultimate);
    
    # Start transition to third person camera
    StartCamera(EyePosition: EyePosition(), LookAtPosition: EyePosition() + FacingDirectionOf(), BlendSpeed: 30);
    Wait();
    StartCamera(EyePosition: RayCastHitPosition(WorldVectorOf(Vector(0, 0, 0), EventPlayer(), LocalVector.RotationAndTranslation), WorldVectorOf(Vector(0, 3, -2), EventPlayer(), LocalVector.RotationAndTranslation)), LookAtPosition: WorldVectorOf(Vector(0, 3, 0), EventPlayer(), LocalVector.RotationAndTranslation), BlendSpeed: 30);

    # Universal Effects
    async! CreateHeroEffects();
    PlayEffect(AllPlayers(), PlayEffect.DebuffImpactSound, <Color>TeamOf(), EyePosition(), 500);
    PlayEffect(AllPlayers(), PlayEffect.OrisaHaltImplosionSound, <Color>TeamOf(), EyePosition(), 500);

    # Casting
    (<MoveSpeedModification>MoveSpeedModification.GetMoveSpeedModification(OrisaMoveSpeedID)).Value = 0;
    SetMoveSpeed(MoveSpeedPercent: 0);
    StartFacing(EventPlayer(), Vector(0, 0, 1), 500, Relative.ToPlayer, FacingRev.DirectionAndTurnRate);
    WaitUntil(IsTrueForAny([HasStatus(Status: Status.Asleep), HasStatus(Status: Status.Hacked), HasStatus(Status: Status.KnockedDown), HasStatus(Status: Status.Stunned), HasStatus(Status: Status.Frozen)], ArrayElement() == true), 0.6);

    SetDamageDealt(DamageDealtPercent: 100);

    # Gallop    
    ForceThrottle(MinForward: 1, MaxForward: 1);
    (<MoveSpeedModification>MoveSpeedModification.GetMoveSpeedModification(OrisaMoveSpeedID)).Value = 500;
    SetMoveSpeed(MoveSpeedPercent: 500);
    DisableMovementCollisionWithPlayers();
    PressButton(Button: Button.Melee);
    Galloping = true;

    Player ExcludedPlayers = [];
    Number TimeRemaining = 0;
    
    ChaseVariableAtRate(TimeRemaining, 9999, 1, RateChaseReevaluation.DestinationAndRate);
    while (TimeRemaining < 0.25)
    {
        if (IsTrueForAny([!IsInLineOfSight(EyePosition(), EyePosition() + WorldVectorOf(Forward() * 2, EventPlayer(), LocalVector.Rotation)), HasStatus(Status: Status.Asleep), HasStatus(Status: Status.Hacked), HasStatus(Status: Status.KnockedDown), HasStatus(Status: Status.Stunned), HasStatus(Status: Status.Frozen)], ArrayElement() == true)) {
            # Stop Gallop
            break;
        } else {
            Player TargetPlayers = RemoveFromArray(FilteredArray(PlayersWithinRadius(EyePosition(), 5, OppositeTeamOf(TeamOf()), RadiusLOS.Surfaces), IsInViewAngle(EventPlayer(), EyePosition(ArrayElement()), 90)), ExcludedPlayers);
            Damage(TargetPlayers, EventPlayer(), 30);
            ModifyVariable(ExcludedPlayers, Operation.AppendToArray, TargetPlayers);
        }
        Wait();
    }
    StopChasingVariable(TimeRemaining);

    CleanupEffects();

    # Stop movement
    StopForcingThrottle();
    (<MoveSpeedModification>MoveSpeedModification.GetMoveSpeedModification(OrisaMoveSpeedID)).Value = 0;
    SetMoveSpeed(MoveSpeedPercent: 0);
    EnableMovementCollisionWithPlayers();
    SetAbilityCooldown(Button: Button.Melee, Cooldown: 0);
    Galloping = false;

    Wait(0.4);

    # Start transition into first person camera
    StartCamera(EyePosition: EyePosition(), LookAtPosition: EyePosition() + FacingDirectionOf(), BlendSpeed: 30);
    Wait(0.25);
    StopCamera();

    # Set cooldown / restore movement
    SetAbilityCooldown(Button: Button.Ability2, Cooldown: 10);
    (<MoveSpeedModification>MoveSpeedModification.GetMoveSpeedModification(OrisaMoveSpeedID)).Value = 100;
    SetMoveSpeed(MoveSpeedPercent: 100);
    StopFacing();

    # Re-enable
    SetPrimaryFireEnabled(Enabled: true);
    SetSecondaryFireEnabled(Enabled: true);
    SetAbility2Enabled(Enabled: true);
    AllowButton(Button: Button.Ultimate);
}

void OrisaGallopKnockback() playervar '[orisa.del] Subroutine: Galloping Knockback' {
    ApplyImpulse(Victim(), EventDirection() + (Up() / 2), 20, Relative.ToWorld, ContraryMotion.Cancel);
    SetEnvironmentCreditPlayer(EventPlayer(), Attacker());
}

void OrisaUltimate() playervar '[orisa.del] Subroutine: Ultimate' {
    Number TemporaryCooldown = AbilityCooldown(Button: Button.Ability2);

    CastingUltimate = true;

    # Deploy shield
    SetProjectileSpeed(ProjectileSpeedPercent: 50);
    SetProjectileGravity(ProjectileGravityPercent: AbsoluteValue(1000));
    SetAbilityCooldown(Button: Button.Ability2, Cooldown: 0);
    AllowButton(Button: Button.Ability2);
    PressButton(Button: Button.Ability2);

    # Wait until shield is deployed
    WaitUntil(!IsUsingAbility2(), 9999);

    # Reset
    DisallowButton(Button: Button.Ability2);
    SetAbilityCooldown(Button: Button.Ability2, Cooldown: TemporaryCooldown);
    SetProjectileGravity(ProjectileGravityPercent: 100);
    SetProjectileSpeed(ProjectileSpeedPercent: 125);

    CastingUltimate = false;
}

/* ================================================== */

rule: '[orisa.del] Initial'
Event.OngoingPlayer
Player.Orisa
{
    InitialOrisa();
}

rule: '[orisa.del] Decrease Primary Fire Movement Speed Penalty'
Event.OngoingPlayer
Player.Orisa
if (IsFiringPrimary()) {
    OrisaDecreaseMovementPenalty();
}

rule: '[orisa.del] Gallop'
Event.OngoingPlayer
Player.Orisa
if (!CastingUltimate)
if (IsButtonHeld(Button: Button.Ability2))
if (AbilityCooldown(Button: Button.Ability2) == 0)
{
    OrisaGallop();
}

rule: '[orisa.del] Galloping Knockback'
Event.OnDamageTaken
if (HeroOf(Attacker()) == Hero.Orisa || HeroBeingDuplicated(Attacker()) == Hero.Orisa)
if (Attacker().Galloping)
if (EventAbility() == null)
{
    OrisaGallopKnockback();
}

rule: '[orisa.del] Ultimate'
Event.OngoingPlayer
Player.Orisa
if (IsUsingUltimate())
{
    OrisaUltimate();
}

/* ================================================== */

rule: '[orisa.del] Initial (Duplicate)'
Event.OngoingPlayer
Player.Echo
if (HeroBeingDuplicated() == Hero.Orisa)
{
    InitialOrisa();
}

rule: '[orisa.del] Decrease Primary Fire Movement Speed Penalty (Duplicate)'
Event.OngoingPlayer
Player.Echo
if (HeroBeingDuplicated() == Hero.Orisa)
if (IsFiringPrimary()) {
    OrisaDecreaseMovementPenalty();
}

rule: '[orisa.del] Gallop (Duplicate)'
Event.OngoingPlayer
Player.Echo
if (HeroBeingDuplicated() == Hero.Orisa)
if (!CastingUltimate)
if (IsButtonHeld(Button: Button.Ability2))
if (AbilityCooldown(Button: Button.Ability2) == 0)
{
    OrisaGallop();
}

rule: '[orisa.del] Ultimate (Duplicate)'
Event.OngoingPlayer
Player.Echo
if (HeroBeingDuplicated() == Hero.Orisa)
if (IsUsingUltimate())
{
    OrisaUltimate();
}